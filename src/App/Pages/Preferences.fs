module Pages.Preferences

open Console
open Utils

[<RequireQualifiedAccess>]
type Action =
    | SavePath
    | Language
    | Quality
    | Return

    static member toString x =
        match x with
        | Action.SavePath -> Strings.Strings.GetString "Pages.Preferences.Actions.SavePath"
        | Action.Language -> Strings.Strings.GetString "Pages.Preferences.Actions.Language"
        | Action.Quality -> Strings.Strings.GetString "Pages.Preferences.Actions.Quality"
        | Action.Return -> Strings.Strings.GetString "Pages.Preferences.Actions.Return"

    static member fromString x =
        match x with
        | s when s = Strings.Strings.GetString "Pages.Preferences.Actions.SavePath" -> Action.SavePath
        | s when s = Strings.Strings.GetString "Pages.Preferences.Actions.Language" -> Action.Language
        | s when s = Strings.Strings.GetString "Pages.Preferences.Actions.Quality" -> Action.Quality
        | s when s = Strings.Strings.GetString "Pages.Preferences.Actions.Return" -> Action.Return
        | _ -> failwith $"Unknown action {x}"

let askForSavePath (defaultPath: string) =
    TextPrompt.create (Strings.Strings.GetString "Pages.Preferences.AskSavePath") defaultPath
    |> Console.prompt

let askForQuality (defaultQuality: Preferences.Quality) =
    MenuPrompt.create<Preferences.Quality>
        (Strings.Strings.GetString "Pages.Preferences.AskQuality")
        (DiscriminatedUnion.listCases<Preferences.Quality> ())
        Preferences.Quality.toString
    |> Console.prompt

let askForLanguage (defaultQuality: Preferences.Language) =
    MenuPrompt.create<Preferences.Language>
        (Strings.Strings.GetString "Pages.Preferences.AskLanguage")
        (DiscriminatedUnion.listCases<Preferences.Language> ())
        Preferences.Language.toString
    |> Console.prompt

let updateSavePath savePath = Preferences.updateSavePath savePath

let updateQuality quality = Preferences.updateQuality quality

let updateLanguage language = Preferences.updateLanguage language

let getCurrentPreferences = Preferences.loadPreferences

let renderPreferencesTable (preferences: Preferences.Preferences) =
    Table.create [ Strings.Strings.GetString "Console.TableHeaders.SavePath"
                   Strings.Strings.GetString "Console.TableHeaders.ImageQuality"
                   Strings.Strings.GetString "Console.TableHeaders.Language" ]
    |> Table.addRow [ preferences.SavePath
                      preferences.Quality
                      preferences.Language ]

let showActions () =
    Console.clear ()

    Strings.Strings.GetString "Pages.Preferences.CurrentPreferencesHeader" |> Console.echo

    getCurrentPreferences ()
    |> renderPreferencesTable
    |> Console.render

    MenuPrompt.create<Action>
        (Strings.Strings.GetString "Pages.Preferences.SelectSettingPrompt")
        (DiscriminatedUnion.listCases<Action> ())
        Action.toString
    |> Console.prompt

let rec handleAction returnAction action =
    let refresh () = initialize returnAction
    let currentPreferences = getCurrentPreferences ()

    action
    |> function
        | Action.SavePath ->
            currentPreferences.SavePath
            |> askForSavePath
            |> updateSavePath
            <| currentPreferences
            |> Preferences.storePreferences
            |> refresh
        | Action.Quality ->
            currentPreferences
            |> Preferences.getQuality
            |> askForQuality
            |> updateQuality
            <| currentPreferences
            |> Preferences.storePreferences
            |> refresh
        | Action.Language ->
            currentPreferences
            |> Preferences.getLanguage
            |> askForLanguage
            |> updateLanguage
            <| currentPreferences
            |> Preferences.storePreferences
            |> refresh
        | _ -> returnAction ()

and initialize returnAction =
    () |> (showActions >> handleAction returnAction)
